#To edit and compare internal_properties, use WINDEV integrated tools.
#Internal properties refer to the properties of controls in windows, reports, etc.
info :
 name : COL_ComptesClient
 major_version : 28
 minor_version : 0
 type : 7
 description : ""
 subtype : 0
procedure_set :
 identifier : 0x1e11e58700ca880f
 internal_properties : CAAAAAgAAABGLu41kG7fjQV3iS4F72qmnKaNh5694reolNKIW0iw
 code_elements :
  internal_properties : CAAAAAgAAACVRvEPjC/Zgj1ZVXXEKoBtZqd/+4DnupRF2xIteC0ZZdkrtIXT1cmFk2W7qaaSBhO0exyc0ZkPGE6HOG92AV9s22h4p3L+hIYxDrQ6mdU42jEiOMVVmL6pNzSQ8zqqugpuWaOZyPdwsYnieyouvhvMi3IlGV4lg1zvhwJWzODK4V0qOMGIQdLgye9U1TCI0g==
  type_code : 31
  p_codes :
   -
     internal_properties : CAAAAAgAAADrzfIJoWaGdg9Mn1oET7DJg50MXChNoxuPcvOgCFXPDxFcLrhzEFEN2IND1Agm3X7XejoQIBSyZL69KF3Q4K6ETTFUm9Xc6LiE7kL8BoJIWmspGDOKm9bnYChNpMcxh7T1dUcxMiNisqQ214w=
     code : |1-
      CONSTANT
      	// Options de la fonction AdresseAffichable
      	ADR_ACTIVE = 0
      	ADR_ARCHIVE = 1
      	MDP_NON_DEFINI = "NonChangé"
      FIN
      
      
      STClient est une Structure
      	IDClient est un entier
      	eMail est une chaîne
      	Civilité est un entier
      	Nom est une chaîne
      	Prénom est une chaîne
      	TelFixe est une chaîne
      	TelPortable est une chaîne
      	DateCréation est une Date
      FIN
      
      
      gbComptesClientsInitialisés est un booléen = Faux
      
      gnAlgorithmeHashPasse est un entier = HA_SHA_256	// Algorithme utilisé pour le hachage des mots de passe (cette variable doit rester fixe pour un site donné)
      gbActivationParEmail est un booléen = Vrai			// Vrai si les comptes utilisateurs sont créés inactifs et doivent être activés par e-mail
      
      // Paramètres pour les envois de messages
      gsLoginSMTP est une chaîne = ""
      gsMotDePasseSMTP est une chaîne = "" 
      gsServeurSMTP est une chaîne = "localhost"
      gnPortSMTP est une chaîne = 25
      gsEmailExpediteurActivation est une chaîne = "activation"
      gsEmailSupportTechnique est une chaîne = "assistance"
      
      // Le nom du site est une chaîne utilisée dans les messages ou les e-mails envoyés depuis le site
      // Sa valeur par défaut est le nom de déploiement du site mais n'importe quelle valeur peut être utilisée
      gsNomSite est une chaîne = ProjetInfo(piNomSiteDéployé)
      
      PRIVÉE
      gnClientEnCours est un entier sur 8	// IDClient du client connecté (0 si pas de client connecté)
     type : 720896
  procedures :
   -
     name : bConnecte
     procedure_id : 2166765414062428132
     type_code : 15
     code : |1+
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      //[ <Résultat> = ] bConnecte (<sIdentifiant> est chaîne, <sMotDePasse> est chaîne)
      //
      // Paramètres :
      //	sIdentifiant (chaîne) : <indiquez ici le rôle de sIdentifiant>
      //	sMotDePasse (chaîne) : <indiquez ici le rôle de sMotDePasse>
      // Valeur de retour :
      // 	booléen : // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCÉDURE bConnecte( sIdentifiant est une chaîne, sMotDePasse est une chaîne )
      
      // L'identifiant peut être un e-mail ou un nom d'affichage
      SI HLitRecherchePremier(Client,eMail,sIdentifiant) = Faux ALORS
      	// Utilisateur inconnu
      	RENVOYER Faux
      FIN
      
      // L'utilisateur a été trouvé, il faut vérifier son mot de passe
      sHashPasse est une chaîne = sHashMotDePasse(sMotDePasse)
      SI Client.MotDePasse<>sHashPasse ALORS
      	// Mot de passe incorrect
      	RENVOYER Faux
      FIN
      
      // Le mot de passe est valide, on connecte l'utilisateur en recopiant son identifiant dans la variable du composant
      gnClientEnCours = Client.IDClient
      
      RENVOYER Vrai
      
      
     type : 458752
   -
     name : bEstConnecte
     procedure_id : 2166765723300145532
     type_code : 15
     code : |1-
      // Résumé : Indique si l'utilisateur est connecté au site
      // Syntaxe :
      //[ <Résultat> = ] bEstConnecte ()
      //
      // Valeur de retour :
      // 	booléen : Vrai si l'utilisateur s'est connecté au site
      //
      PROCÉDURE bEstConnecte()
      
      RENVOYER (gnClientEnCours<>0)
     type : 458752
   -
     name : Deconnecte
     internal_properties : CAAAAAgAAACyU2x9rFVIgfUhDRbnOfs6io9hD7DSU+nnnuctvnCwWZH/Zy6T1LHdO4i7gxipbaLoy6NgfU12ahiTV31BwAsNQwNctNDcnJgOFGyBZJ8jmm6+q6UIVSbyKCmpXJueZg/MXLpWQ1e3eVd+iGihBg==
     procedure_id : 2166765723300211162
     type_code : 15
     code : |1+
      // Résumé : Déconnecte l'utilisateur en cours
      // Syntaxe :
      // Deconnecte ()
      //
      PROCÉDURE Deconnecte()
      
       // Efface la globale de l'identifiant
      gnClientEnCours = 0
     type : 458752
   -
     name : EnvoiEmailRappelMotDePasse
     procedure_id : 2166765723300276807
     type_code : 15
     code : |1-
      // Résumé : Envoi un message de rappel de mot de passe
      // Syntaxe :
      //EnvoiEmailRappelMotDePasse (<nIDUtil> est entier sur 8 octets, <sNouveauMotDePasse> est chaîne)
      //
      // Paramètres :
      //	nIDUtil (entier sur 8 octets) :Identifiant de l'utilisateur
      //	sNouveauMotDePasse (chaîne ANSI) :Nouveau mot de passe
      
      PROCÉDURE EnvoiEmailRappelMotDePasse( nIDUtil est un entier sur 8, sNouveauMotDePasse est une chaîne )
      
      cMel est un Email
      
      // Se positionne sur l'enregistrement de l'utilisateur
      HLitRecherchePremier(Client,IDClient,nIDUtil)
      
      cMel.Sujet = ChaîneConstruit("Réinitialisation de votre mot de passe %1",gsNomSite)
      cMel.Expediteur = gsEmailExpediteurActivation
      Ajoute(cMel.Destinataire,Client.eMail)
      cMel.Message = ChaîneConstruit([
      Bonjour,
      
      Votre mot de passe sur le site %1 a été réinitialisé.
      Votre nouveau mot de passe est : %2
      
      Cordialement,
      L'équipe %1
      ],gsNomSite,sNouveauMotDePasse)
      
      EnvoiEmail(cMel)
     type : 458752
   -
     name : nClientEnCours
     procedure_id : 2166765727595309732
     type_code : 15
     code : |1+
      // Résumé : Retourne l'identifiant de l'utilisateur connecté
      // Syntaxe :
      //[ <Résultat> = ] nIDUtilisateur ()
      //
      // Paramètres :
      //	Aucun
      
      PROCÉDURE nClientEnCours()
      
      RENVOYER gnClientEnCours
     type : 458752
   -
     name : ReinitialiseMotDePasse
     internal_properties : CAAAAAgAAAACIxwJpUn4ucEdO7BdVeMy9guNI1pGs0lbGuVLEDjM7V0Dry6p7FWx34zdKbK5NUrIK880842Kzbn0ygICwdZKbHQdQ2VB2Rc7wYce5bB+B8vbGJT5vt8tAVJY1WT3tdgFHQsthDiIcEySmBgRtg==
     procedure_id : 2166765727595375346
     type_code : 15
     code : |1+
      // Résumé : Réinitialise le mot de passe d'un utilisateur, désactive le compte et envoi le mail d'activation
      // Syntaxe :
      //ReinitialiseMotDePasse (<sIDUtilisateur> est chaîne)
      //
      // Paramètres :
      //	sIDUtilisateur (chaîne) : Nom ou e-mail de l'utilisateur
      
      PROCÉDURE ReinitialiseMotDePasse( sIDUtilisateur est une chaîne )
      
      // Vérifie que le compte existe et qu'il est actif
      SI PAS bVerifieIdentifiantDisponible(sIDUtilisateur,Vrai) ALORS
      	RETOUR
      FIN
      
      // Génère un nouveau mot de passe
      sNouveauMDP est une chaîne
      BOUCLE (8)
      	sNouveauMDP += Caract(47+Hasard(75))
      FIN
      
      // Réinitialise le compte
      Client.MotDePasse = sHashMotDePasse(sNouveauMDP)
      HModifie(Client)
      
      // Envoi l'e-mail d'activation
      EnvoiEmailRappelMotDePasse(Client.IDClient,sNouveauMDP)
      
      Info(<§@1e11e57d00c85e220001§>)
     type : 458752
   -
     name : sHashMotDePasse
     internal_properties : CAAAAAgAAACs/oBOATSciLJS++MSCi2Kd5181aepoKQ3T1KJpNJMLyKLFlUhTzXWr6aOmegs1b249eGCSsrAtxb0eQ083TlunQF44ZttmnkeMQgjiTDlm4hEU+ZvxioNZqw70AZ5thxu8m7DrhCXILEEaxN3
     procedure_id : 2166765727595440976
     type_code : 15
     code : |1-
      // Résumé : Crypte un mot de passe et encode le résultat en Base64
      // Syntaxe :
      //[ <Résultat> = ] sHashMotDePasse (<sMotDePasse> est chaîne)
      //
      // Paramètres :
      //	sMotDePasse (chaîne) : mot de passe
      // Valeur de retour :
      // 	variant : // 	chaine
      //
      PROCÉDURE sHashMotDePasse( sMotDePasse est une chaîne )
      
      RENVOYER Crypte(HashChaîne(gnAlgorithmeHashPasse,sMotDePasse),"",compresseAucun,encodeBASE64)
     type : 458752
   -
     name : InitialiseGestionClients
     internal_properties : CAAAAAgAAAAaKxQVJTlsOUHdOzDdVaPyNsvN49oGMwkbGiULUHhM7d2DL65pbBUx30xdKbI5dQoI6w90880KLVlUKqKiYfbqjNQ9Y0Whefdboec+hVCeJ+s7uLTZnn+NITI49QSXVfglfSvN5JioBLCmoBAJrg==
     procedure_id : 2166766118437556625
     type_code : 15
     code : |1-
      // Résumé : Permet d'initialiser la gestion des clients
      // Syntaxe :
      //[ <Résultat> = ] InitialiseGestionClients ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	booléen : Vrai si l'initialisation a été effectuée correctement, faux sinon
      
      PROCÉDURE InitialiseGestionClients()
      
      DéclareContexteAWP(gnAlgorithmeHashPasse )
      DéclareContexteAWP(gbActivationParEmail )
                        
      // Paramètres pour les envoi
      DéclareContexteAWP(gsLoginSMTP)
      DéclareContexteAWP(gsMotDePasseSMTP)
      DéclareContexteAWP(gsServeurSMTP)
      DéclareContexteAWP(gnPortSMTP )
      DéclareContexteAWP(gsEmailExpediteurActivation )
      DéclareContexteAWP(gsEmailSupportTechnique)
                        
      // Initialisation déjà faite ?
      DéclareContexteAWP(gbComptesClientsInitialisés)
      DéclareContexteAWP(gnClientEnCours)
      
      SI gbComptesClientsInitialisés = Faux ALORS
      	// l'initialisation n'avait pas encore été faite, on note qu'elle a été faite
      	gbComptesClientsInitialisés = Vrai
      	RENVOYER Vrai
      FIN
      
      // C'est pas l'init, on a remis les valeurs comme elles avaient été sauvegardées
      RENVOYER Faux
     type : 458752
   -
     name : bVerifieIdentifiantDisponible
     internal_properties : CAAAAAgAAAC1ZSRObXdGaUUEeAUByRMI7PYZAyqK6CKg2H0f5yTPcrp9Unfaobu2w4TE0c/3WAT9l6+WqKuJ+r48xTZ3RfxmGJm6xSrNMCddZneGpASYXQeo29m3G4CQLl5RQ20mSPlv4o8iWB3VgmcqaFpRDzub0LxEbXWVVxzdmbBbWiV6XiIbZqNCUhbK8JZbcelDQNiVbCFmo2HCDJ2A1CjbThq96UzXuAY1a0cd7LK4YZyvJwrHazt6RhUSuy+g09rjxbUTSShgN0QI1hYrMmUUb38NkjWzdBsJIhHh3T9ILxbtnTXHsByJONlBiLhcLKuD/kNUrOMUwiqhit7Y08+GILQIa6P75rp1hQ5hXg/BZqtTM1uCqmFuSsHwdbmz
     procedure_id : 2166768012518181445
     type_code : 15
     code : |1-
      // Résumé : Vérifie qu'un identifiant n'est pas déjà utilisé (comme e-mail ou nom d'affichage)
      // Syntaxe :
      //[ <Résultat> = ] bVerifieIdentifiantDisponible (<sIdentifiant> est chaîne [, <bBloqueEnreg> est booléen])
      //
      // Paramètres :
      //	sIdentifiant (chaîne) : Chaîne à vérifier
      //	bBloqueEnreg (booléen - valeur par défaut=0) : indique si la lecture doit bloquer l'enregistrement
      // Valeur de retour :
      // 	booléen : Vrai si l'identifiant n'existe ni en tant qu'e-mail, ni en tant que nom d'affichage
      //
      PROCÉDURE bVerifieIdentifiantDisponible( sIdentifiant est une chaîne, bBloqueEnreg est un booléen = Faux )
      
      nFlag est un entier = hBlocageNon
      SI bBloqueEnreg ALORS nFlag = hBlocageEcriture
      
      // Vérification des e-mails
      SI HLitRecherchePremier(Client,eMail,sIdentifiant,nFlag) ALORS
      	RENVOYER Faux
      FIN
      
      // Vérification des noms d'affichage
      SI HLitRecherchePremier(Client,Nom,sIdentifiant,nFlag) ALORS
      	RENVOYER Faux
      FIN
      
      // L'identifiant est libre
      RENVOYER Vrai
     type : 458752
   -
     name : AdresseAffichable
     procedure_id : 2167503195004416927
     type_code : 15
     code : |1-
      // Résumé :  Permet de récupérer une adresse affichage
      // Syntaxe :
      //[ <Résultat> = ] AdresseAffichable (<nIDAdresse> est entier sur 8 octets, <nOption> est entier)
      //
      // Paramètres :
      //	nIDAdresse (entier sur 8 octets) : L'identifiant de l'adresse
      //	nOption (entier) : < indiquez ici le rôle de bAdresseArchivée >
      
      PROCÉDURE AdresseAffichable(nIDAdresse est un entier sur 8 octets, nOption est un entier )
      
      bAdresseArchivée est un booléen = (ETBinaire(nOption,ADR_ARCHIVE)>0)
      
      sdAdresse est une Source de Données
      sAdresse est une chaîne
      // On crée un alias sur le bon fichier et c'est cet alias que l'on va manipuler
      SI bAdresseArchivée ALORS
      	HAlias(AdresseArchive,sdAdresse)
      	HChangeNom(sdAdresse, AdresseArchive..NomPhysique)
      SINON
      	HAlias(Adresse,sdAdresse)
      	HChangeNom(sdAdresse, Adresse..NomPhysique)
      FIN
      
      // Recherche de l'adresse
      HLitRecherchePremier(sdAdresse,IDAdresse,nIDAdresse)
      SI HTrouve(sdAdresse) ALORS
      	// Formatage de l'adresse
      	sAdresse = sdAdresse.Societe
      	sAdresse += [RC] + sdAdresse.Voie
      	sAdresse += [RC] + sdAdresse.Complement
      	sAdresse += [RC] + sdAdresse.LieuDitBP
      	sAdresse += [RC] + sdAdresse.CodePostal + [" "] + sdAdresse.Ville
      	sAdresse += [RC] + sdAdresse.EtatProvince
      	sAdresse += [RC] + sdAdresse.Pays
      FIN
      
      // on renvoie l'adresse formatée
      RENVOYER sAdresse
      
      FIN:
      HAnnuleAlias(sdAdresse)
     type : 458752
   -
     name : CivilitéVersChaine
     procedure_id : 2167851916271032879
     type_code : 15
     code : |1-
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      //[ <Résultat> = ] CivilitéVersChaine (<nCivilité> [, <bAbrégée>])
      //
      // Paramètres :
      //	nCivilité : <indiquez ici le rôle de nCivilité>
      // 	bAbrégée (valeur par défaut=1) : < indiquez ici le rôle de bAbrégée >
      // Valeur de retour :
      // 	chaîne : // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCÉDURE CivilitéVersChaine(nCivilité, bAbrégée = Vrai)
      
      SI bAbrégée ALORS
      	SELON nCivilité
      		CAS 1 // Monsieur
      			RENVOYER "M."
      		CAS 2 // Madame
      			RENVOYER "Mme"
      		CAS 3 // Mademoiselle
      			RENVOYER "Mlle"
      		AUTRE CAS
      			dbgAssertion(Faux,"Civilité inconnue")
      			RENVOYER ""
      	FIN
      SINON
      	SELON nCivilité
      		CAS 1 // Monsieur
      			RENVOYER "Monsieur"
      		CAS 2 // Madame
      			RENVOYER "Madame"
      		CAS 3 // Mademoiselle
      			RENVOYER "Mademoiselle"
      		AUTRE CAS
      			dbgAssertion(Faux,"Civilité inconnue")
      			RENVOYER ""
      	FIN
      	
      FIN
     type : 458752
   -
     name : VérifieConnexion
     procedure_id : 2167860437496193295
     type_code : 15
     code : |1-
      // Résumé : <indiquez ici ce que fait la procédure>
      // Syntaxe :
      //VérifieConnexion ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCÉDURE VérifieConnexion()
      
      SI bEstConnecte() = Faux ALORS
      	ScriptAffiche(COL_ECommerce.gsURLPageConnexion)
      
      FIN
     type : 458752
   -
     name : nNbAdresse
     internal_properties : CAAAAAgAAACIWaJA59kEAWVRDRaH2VtKip8R/xDio1lHHte9roDguQHfhw5D1JE9Kzh7g/hJrVLomzOQPd1GUsD7T1VJmBOFK6vkLGikpKA23CS5TLdr8tbmE53gnc4aUDGhdON2LkeU5GJuW3+fLe/FTqKbPA==
     procedure_id : 2168216447335534728
     type_code : 15
     code : |1-
      // Résumé : Retourne le nombre d'adresses pour le client en cours
      // Syntaxe :
      //[ <Résultat> = ] nNbAdresse ()
      //
      // Paramètres :
      //	Aucun
      // Valeur de retour :
      // 	Type indéterminé : // 	Aucune
      //
      // Exemple :
      // Indiquez ici un exemple d'utilisation.
      //
      PROCÉDURE nNbAdresse()
      
      dbgAssertion(gnClientEnCours<>0,"Il faut être connecté pour appeler cette fonction")
      
      SI HExécuteRequête(REQ_NombreAdresseClient,hRequêteDéfaut,gnClientEnCours) ALORS
      	RENVOYER REQ_NombreAdresseClient.nNbAdresse
      FIN
      
      dbgAssertion(Faux,"Echec de l'exécution de la requête")
      RENVOYER 0
     type : 458752
   -
     name : AdresseNomComplet
     procedure_id : 2235700777370345153
     type_code : 15
     code : |1-
      // Résumé :  Permet de récupérer une adresse affichage
      // Syntaxe :
      //[ <Résultat> = ] AdresseNomComplet (<nIDAdresse> est entier sur 8 octets, <nOption> est entier)
      //
      // Paramètres :
      //	nIDAdresse (entier sur 8 octets) : L'identifiant de l'adresse
      //	nOption (entier) : <indiquez ici le rôle de nOption>
      
      PROCÉDURE AdresseNomComplet(nIDAdresse est un entier sur 8 octets, nOption est un entier )
      
      bAdresseArchivée est un booléen = (ETBinaire(nOption,ADR_ARCHIVE)>0)
      
      sdAdresse est une Source de Données
      
      // On crée un alias sur le bon fichier et c'est cet alias que l'on va manipuler
      SI bAdresseArchivée ALORS
      	HAlias(AdresseArchive,sdAdresse)
      	HChangeNom(sdAdresse, AdresseArchive..NomPhysique)
      SINON
      	HAlias(Adresse,sdAdresse)
      	HChangeNom(sdAdresse, Adresse..NomPhysique)
      FIN
      
      // Recherche de l'adresse
      HLitRecherchePremier(sdAdresse,IDAdresse,nIDAdresse)
      SI HTrouve(sdAdresse) ALORS
      	// Formatage de l'adresse
      	// on renvoie l'adresse formatée
      	RENVOYER sdAdresse.NomComplet
      
      FIN
      
      dbgAssertion(Faux,"Adresse introuvable")
      RENVOYER ""
      
      
      FIN:
      HAnnuleAlias(sdAdresse)
     type : 458752
   -
     name : RécupèreClient
     procedure_id : 1586065048106664176
     type_code : 15
     code : |1+
      PROCÉDURE RécupèreClient( nIDClient est un entier sur 8 )
      
      dbgAssertion(nIDClient<>0)
      
      stInfoClient est un STClient
      SI HLitRecherchePremier(Client,IDClient,nIDClient) ALORS
      	FichierVersMémoire(stInfoClient,Client)
      FIN
      
      RENVOYER stInfoClient
      
     type : 458752
   -
     name : EnvoiEmail
     internal_properties : CAAAAAgAAADYejyvQ+fT2CA7STYZd4D4Om34SQZtGajGIF0swr0H7WvyFGVtxhZ0NzMf1mxsp4jsLCqK3f0m8y65UNolfOY6+vesx/yDK/jnvrZw/8e4VnndflFKWy68YL3Ok582k11GrbtY3v9S+IlT2n24VAgAJAJ5+QgtXRN5HVD2Kx9EYhEUw8U5rUjNNGGoJEJutVPLRHZWYHqaW94up1VSz8PXOGTNjoshPZPKPt+2W8ZAgQZVLwVkkKJdAECfB6M=
     procedure_id : 1612752068195552122
     type_code : 15
     code : |1+
      // Résumé : Procédure centralisant l'envoi d'un email
      // Syntaxe :
      //[ <Résultat> = ] EnvoiEmail (<cMel> est Email)
      //
      // Paramètres :
      //	cMel (Email) : message à envoyer
      // Valeur de retour :
      // 	booléen : Faux en cas d'erreur
      //
      PROCÉDURE EnvoiEmail( cMel est un Email )
      
      bRes est un booléen
      
      // Passe le time out à 3 seconde (par défaut le serveur smtp est sur localhost, donc s'il met plus de 3 secondes à répondre, il y a un souci)
      EmailChangeTimeOut(3)
      
      SI EmailOuvreSessionSMTP(gsLoginSMTP,gsMotDePasseSMTP,gsServeurSMTP,gnPortSMTP) ALORS
      	
      	bRes = EmailEnvoieMessage(gsLoginSMTP,cMel)
      	EmailFermeSession(gsLoginSMTP)
      SINON
      	bRes = Faux
      FIN
      
      RENVOYER bRes
     type : 458752
  procedure_templates : []
  property_templates : []
 code_parameters :
  internal_properties : CAAAAAgAAACVGQgYbalU7DKC3oH4ItDrjtcTHXXV/EuH8q0IbhyQlw==
  original_name : COL_SansNom1
resources :
 string_res :
  identifier : 0x1e11e57d00c85e22
  internal_properties : CAAAAAgAAACm76HWfKGWp33VjXInA4cRlqArlgTTA862QGt72W2ld5Y=
  strings :
   -
     text :
      fr-FR : Un nouveau mot de passe a été généré et vous a été envoyé par email.
     index : 1
custom_note :
 internal_properties : CAAAAAgAAABtB9HWVzrXO2+4NDRVK0vmzaNKrCKqH1DBX30lMmGZ
